== [#abs]#ABS#

A function giving the absolute value of its argument.

[source,console]
----
X = ABS(deficit)
length = ABS(X1-X2)
----

This function converts negative numbers into positive ones. It can be used to give the difference between two numbers without regard to the sign of the answer.

It is particularly useful when you want to know the difference between two values, but you don't know which is the larger. For instance, if X=6 and Y=10 then the following examples would give the same result.

[source,console]
----
difference = ABS(X-Y)
difference = ABS(Y-X)
----

You can use this function to check that a calculated answer is within certain limits of a specified value. For example, suppose you wanted to check that 'answer' was equal to 'ideal' plus or minus (up to) 0.5. One way would be:

[source,console]
----
IF answer>ideal-0.5 AND answer<ideal+0.5 THEN----
----

However, the following example would be a more elegant solution.

[source,console]
----
IF ABS(answer-ideal)<0.5 THEN----
----

=== Syntax

[source,console]
----
<n-var>=ABS(<numeric>)
----

=== Associated Keywords

[source,console]
----
SGN
----

'''''

== [#acs]#ACS#

A function giving the arc cosine of its argument in radians. The permitted range of the argument is -1 to +1.

If you know the cosine of the angle, this function will tell you the angle (in radians). Unfortunately, you cannot do this with complete certainty because two angles within the range +/- PI (+/- 180 degrees) can have the same cosine. This means that one cosine has two associated angles.

The following diagram illustrates the problem:

image:acs.png[Arc cosine,width=352,height=236]

Within the four quadrants, there are two angles which have the same cosine, two with the same sine and two with the same tangent. When you are working back from the cosine, sine or tangent you don't know which of the two possible angles is correct.

By convention, ACS gives a result in the top two quadrants (0 to PI - 0 to 180 degrees) and link:#asn[ASN] and link:#atn[ATN] in the right-hand two quadrants (-PI/2 to +PI/2 - -90 to + 90 degrees).

In the example below, 'radian_angle' becomes equal to the angle (in radians) whose cosine is 'y'.

[source,console]
----
radian_angle=ACS(y)
----

You can convert the answer to degrees by using the link:#deg[DEG] function (or multiplying by 180/PI).

[source,console]
----
degree_angle=DEG(ACS(y))
----

=== Syntax

[source,console]
----
<n-var>=ACS(<numeric>)
----

=== Associated Keywords

[source,console]
----
ASN, ATN, SIN, COS, TAN, RAD, DEG
----

== [#adval]#ADVAL#

This function returns information about the analogue to digital converter channels on the Games Adapter card (if fitted). The information returned depends upon the argument passed to ADVAL.

*_Not implemented_*

=== Syntax

[source,console]
----
<n-var>=ADVAL(<numeric>)
----

=== Associated Keywords
[source,console]
----
SOUND, INPUT, GET
----

== [#and]#AND, A.#

The operation of integer bitwise logical AND between two items. The 2 operands are internally converted to four byte integers before the AND operation.

[source,console]
----
answer=num1 AND num2
char=byte AND &7F

IF (num AND &F0)

test=(count=3 AND total=5)
----

You can use AND as a logical operator or as a 'bit-by-bit' (bitwise) operator. The operands can be boolean (logical) or numeric.

In the following example program segment, AND is used as a bitwise operator to remove the most significant bit of a byte read from a file before writing it to another file. This is useful for converting some word-processor files into standard link:annexa.html[ASCII] format.

[source,console]
----
210 byte=BGET#infile AND &7F
220 BPUT#outfile,byte
----

Unfortunately, BBC BASIC does not have true boolean variables; it uses numeric variables and assigns the value 0 for link:bbckey2.html#false[FALSE] and -1 for link:bbckey4.html#true[TRUE]. This can lead to confusion at times. (See link:bbckey3.html#not[NOT] for more details.) In the example below, the operands are boolean (logical). In other words, the result of the tests (IF) A=2 and (IF) B=3 is either TRUE or FALSE. The result of this example will be TRUE if A=2 and B=3.

[source,console]
----
answer=(A=2 AND B=3)
----

The brackets are not necessary, they have been included to make the example easier to follow.

The second example is similar to the first, but in the more familiar surroundings of an IF statement.

[source,console]
----
IF A=2 AND B=3 THEN 110
----

or
[source,console]
----
answer= A=2 AND B=3     `(without brackets this time)` IF answer THEN 110      
----

The final example, uses the AND in a similar fashion to the numeric operators (+, -, etc).

[source,console]
----
`A=X AND 11`
----

Suppose X was -20, the AND operation would be:

[source,console]
----
11111111 11111111 11111111 11101100
00000000 00000000 00000000 00001011
00000000 00000000 00000000 00001000  = 8
----

=== Syntax

[source,console]
----
<n-var>=<numeric> AND <numeric>
----

=== Associated Keywords
[source,console]
----
EOR, OR, FALSE, TRUE, NOT
----

'''''

== [#asc]#ASC#

A function returning the ASCII character value of the first character of the argument string. If the string is null then -1 will be returned.

A computer only understands numbers. In order to deal with characters, each character is assigned a code number. For example (in the link:annexa.html[ASCII code table]) the character 'A' is given the code number 65 (decimal). A part of the computer generates special electronic signals which cause the characters to be displayed on the screen. The signals generated vary according to the code number.

You could use this function to convert ASCII codes to some other coding scheme.

[cols=",",]
|==
|`ascii_code=ASC("H")` |Result would be 72
|`X=ASC("HELLO")` |Result would be 72
| |
|`name$="FRED"` |
|`ascii_code=ASC(name$)` |Result would be 70
|`X=ASC"e"` |Result would be 101
| |
|`X=ASC(MID$(A$,position))` |Result depends on A$ and position.
|==

ASC is the complement of link:#chr[CHR$].

=== Syntax

[source,console]
----
<n-var>=ASC(<str>)
----

=== Associated Keywords

[source,console]
----
CHR$, STR$, VAL
----

== [#asn]#ASN#

A function giving the arc sine of its argument in radians. The permitted range of the argument is -1 to +1.

By convention, the result will be in the range -PI/2 to +PI/2 (-90 to +90 degrees).

If you know the sine of the angle, this function will tell you the angle (in radians). Unfortunately, you cannot do this with complete certainty because one sine has two associated angles. (See link:#acs[ACS] for details.)

In the example below, 'radian_angle' becomes equal to the angle (in radians) whose sine is 'y'.

[source,console]
----
radian_angle=ASN(y)
----

You can convert the answer to degrees by using the link:#deg[DEG] function. (The DEG function is equivalent to multiplying by 180/PI.) The example below is similar to the first one, but the angle is in degrees.

[source,console]
----
degree_angle=DEG(ASN(y))
----

=== Syntax

[source,console]
----
<n-var>=ASN(<numeric>)
----

=== Associated Keywords

[source,console]
----
ACS, ATN, SIN, COS, TAN, RAD, DEG
----

== [#atn]#ATN#

A function giving the arc tangent of its argument in radians. The permitted range of the argument is from - to + infinity.

By convention, the result will be in the range -PI/2 to +PI/2 (-90 to +90 degrees).

If you know the tangent of the angle, this function will tell you the angle (in radians).

As the magnitude of the argument (tangent) becomes very large (approaches + or - infinity) the accuracy diminishes.

In the example below, 'radian_angle' becomes equal to the angle (in radians) whose tangent is 'y'.

[source,console]
----
radian_angle=ATN(y)
----

You can convert the answer to degrees by using the link:#deg[DEG] function. (The DEG function is equivalent to multiplying by 180/PI.) The example below is similar to the first one, but the angle is in degrees.

[source,console]
----
degree_angle=DEG(ATN(y))
----

=== Syntax

[source,console]
----
<n-var>=ATN(<numeric>)
----

=== Associated Keywords

[source,console]
----
ACS, ASN, SIN, COS, TAN, RAD, DEG
----

== [#auto]#AUTO#, AU.

A command allowing the user to enter lines without first typing in the number of the line. The line numbers are preceded by the usual prompt (>).

You can use this command to tell the computer to type the line numbers automatically for you when you are entering a program (or part of a program).

If AUTO is used on its own, the line numbers will start at 10 and go up by 10 for each line. However, you can specify the start number and the value by which the line numbers will increment. The step size can be in the range 1 to 255.

You cannot use the AUTO command within a program or a multi-statement command line.

You can leave the AUTO mode by pressing the escape key.

[source,console]
----
AUTO start_number,step_size
----

[cols=",",]
|==
|`AUTO` |offers line numbers 10, 20, 30 ...
|`AUTO 100` |starts at 100 with step 10
|`AUTO 100,1` |starts at 100 with step 1
|`AUTO ,2` |starts at 10 with step 2
|==

A hyphen is an acceptable alternative to a comma.

=== Syntax

[source,console]
----
AUTO [<n-const> [,<n-const>]]
----

=== Associated Keywords
[source,console]
----
None
----

== [#bget]#BGET##, B.

A function which gets a byte from the file whose file handle is its argument. The file pointer is incremented after the byte has been read.

[source,console]
----
E=BGET#n
aux=BGET#3
----

You must normally have opened a file using link:bbckey3.html#openout[OPENOUT], link:bbckey3.html#openin[OPENIN] or link:bbckey3.html#openup[OPENUP] before you use this statement. (See these keywords and the link:bbcfile1.html[BBC BASIC (Z80) Disk Files] section for details.) BGET# may alternatively be used with the AUX device (usually a serial port) which has the 'permanently open' handle = 3.

You can use BGET# to read single bytes from a disk file. This enables you to read back small integers which have been 'packed' into fewer than 5 bytes (see link:#bput[BPUT#]). It is also very useful if you need to perform some conversion operation on a file. Each byte read is numeric, but you can use link:#chr[CHR$](BGET#n) to convert it to a string.

The input file in the example below is a text file produced by a word-processor.

Words to be underlined are 'bracketed' with ^S. The program produces an output file suitable for a printer which expects such words to be bracketed by ^Y. You could, of course, perform several such translations in one program.

[source,console]
----
10 REM Open i/p and o/p files. End if error.
 20 infile=OPENIN "WSFILE.DOC"
 30 IF infile=0 THEN END
 40 outfile=OPENOUT "BROTH.DOC"
 50 IF outfile=0 THEN END
 60 :
 70 REM Process file, converting ^S to ^Y
 80 REPEAT
 90   temp=BGET#infile :REM Read byte
100    IF temp=&13 THEN temp=&19 :REM Convert ^S
110    BPUT#outfile,temp :REM Write byte
120 UNTIL temp=&1A :REM ^Z
130 CLOSE#0 :REM Close all files
140 END
----

To make the program more useful, it could ask for the names of the input and output files at 'run time':

[source,console]
----
10INPUT "Enter name of INPUT file " infile$
 20 INPUT "Enter name of OUTPUT file " outfile$
 30 REM Open i/p and o/p files. End if error.
 40 infile=OPENIN(infile$)
 50 IF infile=0 THEN END
 60 outfile=OPENOUT(outfile$)
 70 IF outfile=0 THEN END
 80 :
 90 REM Process file, converting ^S to ^Y
100 REPEAT
110    temp=BGET#infile :REM Read byte
120    IF temp=&13 THEN temp=&19 :REM Convert ^S
130    BPUT#outfile,temp :REM Write byte
140 UNTIL temp=&1A :REM ^Z
150 CLOSE#0 :REM Close all files
160 END
----

=== Syntax

[source,console]
----
<n-var>=BGET#<numeric>
----

=== Associated Keywords

[source,console]
----
OPENIN, OPENUP, OPENOUT, CLOSE#, PRINT#, INPUT#, BGET#, EXT#, PTR#, EOF#
----

== [#bput]#BPUT##, BP.

A statement which puts a byte to the data file whose file handle is the first argument. The second argument's least significant byte is written to the file. The file pointer is incremented after the byte has been written.

[source,console]
----
BPUT#E,32
BPUT#staff_file,A/256
BPUT#4,prn
----

Before you use this statement you must normally have opened a file for output using link:bbckey3.html#openout[OPENOUT] or link:bbckey3.html#openup[OPENUP]. (See these keywords and the link:bbcfile1.html[BBC BASIC (Z80) Disk Files] section for details.) BPUT# may alternatively be used with the AUX device (usually a serial port), which has the 'permanently open' handle = 3, or the PRN device (usually a parallel port) which has the 'permanently open' handle = 4.

You can use this statement to write single bytes to a disk file. The number that is sent to the file is in the range 0 to 255. Real numbers are converted internally to integers and the top three bytes are 'masked off'. Each byte written is numeric, but you can use link:#asc[ASC](character$) to convert (the first character of) 'character$' to a number.

The example below is a program segment that 'packs' an integer number between 0 and 65535 (&FFFF) into two bytes, least significant byte first. The file must have already been opened for output and the file handle stored in 'fnum'. The integer variable number% contains the value to be written to the file.

[source,console]
----
100 BPUT#fnum,number% MOD 256
110 BPUT#fnum,number% DIV 256
----

=== Syntax

[source,console]
----
BPUT#<numeric>,<numeric>
----

=== Associated Keywords

[source,console]
----
OPENIN, OPENUP, OPENOUT, CLOSE#, PRINT#, INPUT#, BGET#, EXT#, PTR#, EOF#
----

== [#call]#CALL#, CA.

A statement to call a machine code subroutine.

[source,console]
----
CALL Muldiv,A,B,C,D
CALL &FFE3
CALL 12340,A$,M,J$
----

The processor's A, B, C, D, E, F, H and L registers are initialised to the least significant words of A%, B%, C%, D%, E%, F%, H% and L% respectively (see also link:bbckey4.html#usr[USR]).

=== [#callparms]#Parameter Table#

CALL sets up a table in RAM containing details of the parameters. The IX register is set to the address of this parameter table.

Variables included in the parameter list need not have been declared before the CALL statement.

The parameter types are:

[cols=">,",options="header",]
|==
|Code No |Parameter Type
|0: |byte (8 bits)
|4: |word (32 bits)
|5: |real (40 bits)
|128: |fixed string
|129: |movable string
|==

[cols=",",]
|==
|Number of parameters |1 byte (at IX)
| |
|Parameter type |1 byte (at IX+1)
|Parameter address |2 bytes (at IX+2 IX+3 LSB first)
| |
|Parameter type |)repeated as often as necessary.
|Parameter address |)
|==

Except in the case of a movable string (normal string variable), the parameter address given is the absolute address at which the item is stored. In the case of movable strings (type 129), it is the address of a parameter block containing the current length, the maximum length and the start address of the string, in that order

=== [#callformats]#Parameter Formats#

Integer variables are stored in twos complement format with their least significant byte first.

Fixed strings are stored as the characters of the string followed by a carriage return (&0D).

Floating point variables are stored in binary floating point format with their least significant byte first. The fifth byte is the exponent. The mantissa is stored as a binary fraction in sign and magnitude format. Bit 7 of the most significant byte is the sign bit and, for the purposes of calculating the magnitude of the number, this bit is assumed to be set to one. The exponent is stored as a positive integer in excess 127 format. (To find the exponent subtract 127 from the value in the fifth byte.)

If the exponent of a floating point number is zero, the number is stored in integer format in the mantissa. If the exponent is not zero, then the variable has a floating point value. Thus, an integer can be stored in two different formats in a real variable. For example, 5 can be stored as

[source,console]
----
 00 00 00 05 00   `Integer 
----

or

[source,console]
----
 20 00 00 00 82   `(.5+.125) * 2^3 = 
----

(the `&20` becomes `&A0` because the MSB is always assumed)

In the case of a movable string (normal string variable), the parameter address points to the 'string descriptor'. This descriptor gives the current length of the string, the number of bytes allocated to the string (the maximum length of the string) and the address of the start of the string (LSB first).

=== Syntax

[source,console]
----
CALL <numeric>{,<n-var>|<s-var>}
----

=== Associated Keywords

[source,console]
----
USR
----

== [#chain]#CHAIN#

A statement which loads and runs the program whose name is specified in the argument.

[source,console]
----
CHAIN "GAME1"
CHAIN A$
----

The program file must be in tokenised format.

All but the static variables @% to Z% are link:#clear[CLEAR]ed.

CHAIN sets ON ERROR OFF before chaining the specified program.

link:bbckey4.html#run[RUN] may be used as an alternative to CHAIN.

You can use CHAIN (or RUN) to link program modules together. This allows you to write modular programs which would, if written in one piece, be too large for the memory available.

Passing data between CHAINed programs can be a bit of a problem because COMMON variables cannot be declared and all but the static variables are cleared by CHAIN.

If you wish to pass large amounts of data between CHAINed programs, you should use a data file. However, if the amount of data to be passed is small and you do not wish to suffer the time penalty of using a data file, you can pass data to the CHAINed program by using the indirection operators to store them at known addresses. The safest way to do this is to move link:bbckey2.html#himem[HIMEM] down and store common data at the top of memory.

The following sample program segment moves HIMEM down 100 bytes and stores the input and output file names in the memory above HIMEM. There is, of course, still plenty of room for other data in this area.

[source,console]
----
100 HIMEM=HIMEM-100
110 $HIMEM=in_file$
120 $(HIMEM+13)=out_file$
130 CHAIN "NEXTPROG"
----

=== Syntax

[source,console]
----
CHAIN <str>
----

=== Associated Keywords

[source,console]
----
LOAD, RUN, SAVE
----

== [#chr]#CHR$#

A function which returns a string of length 1 containing the ASCII character specified by the least significant byte of the numeric argument.

[source,console]
----
A$=CHR$(72)
B$=CHR$(12)
C$=CHR$(A/200)
----

CHR$ generates an ASCII character (symbol, letter, number character, control character, etc) from the number given. The number specifies the position of the generated character in the ASCII table (See link:annexa.html[Annex A]). For example:

[source,console]
----
char$=CHR$(65)
----

will set char$ equal to the character 'A'. You can use CHR$ to send a special character to the terminal or printer. (Generally, link:bbckey4.html#vdu[VDU] is better for sending characters to the screen.) For example,

[source,console]
----
CHR$(7)
----

will generate the ASCII character ^G. So,

[source,console]
----
PRINT "ERROR"+CHR$(7)
----

will print the message 'ERROR' and sound the PC's 'bell'.

CHR$ is the complement of link:#asc[ASC].

=== Syntax

[source,console]
----
<s-var>=CHR$(<numeric>)
----

=== Associated Keywords

[source,console]
----
ASC, STR$, VAL, VDU
----

== [#clear]#CLEAR#, CL.

A statement which clears all the dynamically declared variables, including strings. CLEAR does not affect the static variables.

The CLEAR command tells BBC BASIC (Z80) to 'forget' about ALL the dynamic variables used so far. This includes strings and arrays, but the static variables (@% to Z%) are not altered.

You can use the link:bbc2.html#indirection[indirection] operators to store integers and strings at known addresses and these will not be affected by CLEAR. However, you will need to 'protect' the area of memory used. The easiest way to do this is to move link:bbckey2.html#himem[HIMEM] down. See link:#chain[CHAIN] for an example.

=== Syntax

[source,console]
----
CLEAR
----

=== Associated Keywords

[source,console]
----
None
----

'''''

== [#close]#CLOSE##, CLO.

A statement used to close a data file. CLOSE #0 will close all data files.

[source,console]
----
CLOSE#file_num
CLOSE#0
----

You use CLOSE# to tell BBC BASIC (Z80) that you have completely finished with a data file for this phase of the program. Any data still in the file buffer is written to the file before the file is closed.

You can open and close a file several times within one program, but it is generally considered 'better form' not to close a file until you have finally finished with it. However, if you wish to link:#clear[CLEAR] the variables, it is simpler if you close the data files first.

You should also close data files before chaining another program. link:#chain[CHAIN] does not automatically close data files, but it does clear the variables in which the file handles were stored. You can still access the open file if you have used one of the static variables (A% to Z%) to store the file handle. Alternatively, you could reserve an area of memory (by moving link:bbckey2.html#himem[HIMEM] down for example) and use the byte indirection operator to store the file handle. (See the keyword link:#chain[CHAIN] for more details.)

link:#end[END] or 'dropping off' the end of a program will also close all open data files. However, link:bbckey4.html#stop[STOP] does not close data files.

=== Syntax

[source,console]
----
CLOSE#<numeric>
----

=== Associated Keywords

[source,console]
----
OPENIN, OPENUP, OPENOUT, PRINT#, INPUT#, BPUT#, BGET#, EXT#,  PTR#, EOF#
----

== [#clg]#CLG#

A statement which clears the graphics area of the screen and sets it to the currently selected graphics background colour,

=== Syntax

[source,console]
----
CLG
----

=== Associated Keywords

[source,console]
----
CLS, GCOL
----

== [#cls]#CLS#

A statement which clears the text area of the screen and sets it to the currently selected text background colour. The text cursor is moved to the 'home' position (0,0) at the top left-hand corner of the text area.

=== Syntax

[source,console]
----
CLS
----

=== Associated Keywords

[source,console]
----
CLG, COLOUR
----

== [#colour]#COLOUR#, #COLOR#, C.

Change the the current text output colour.

* If c is between 0 and 63, the foreground text colour will be set
* If c is between 128 and 191, the background text colour will be set

Note, since BBC BASIC is British, the British spelling of "colour" is the default. The US spelling is however recognised as an alternative.

The following two commands are only applicable to paletted modes with fewer than 64 colours.

[source,console]
----
COLOUR l,p
----

Set the logical colour l to the physical colour p

[source,console]
----
COLOUR l,r,g,b
----

Set the logical colour l to the physical colour represented by the RGB value where r(ed), g(reen) and b(lue) are each specified as a base 10 integer value in the range 0-255.

=== Syntax

[source,console]
----
COLOUR c
COLOUR l,p
COLOUR l,r,g,b
----

=== Associated Keywords

[source,console]
----
VDU, GCOL, MODE
----


== [#cos]#COS#

A function giving the cosine of its radian argument.

[source,console]
----
X=COS(angle)
----

This function returns the cosine of an angle. The angle must be expressed in radians, not degrees.

Whilst the computer is quite happy dealing with angles expressed in radians, you may prefer to express angles in degrees. You can use the link:bbckey4.html#rad[RAD] function to convert an angle from degrees to radians.

The example below sets Y to the cosine of the angle 'degree_angle' expressed in degrees.

[source,console]
----
Y=COS(RAD(degree_angle))
----

=== Syntax

[source,console]
----
<n-var>=COS(<numeric>)
----

=== Associated Keywords

[source,console]
----
SIN, TAN, ACS, ASN, ATN, DEG, RAD
----


== [#count]#COUNT#, COU.

A function returning the number of characters sent to the output stream (VDU or printer) since the last new line.

[source,console]
----
char_count=COUNT
----

Characters with an link:annexa.html[ASCII] value of less than 13 (carriage return/new-line/enter) have no effect on COUNT.

Because control characters above 13 are included in COUNT, you cannot reliably use it to find the position of the cursor on the screen. If you need to know the cursor's horizontal position use the link:bbckey3.html#pos[POS] function.

Count is NOT set to zero if the output stream is changed using the link:opsys1.html#opt[*OPT] command.

The example below prints strings from the string array 'words$'. The strings are printed on the same line until the line length exceeds 65. When the line length is in excess of 65, a new-line is printed.

[source,console]
----
90 ----
100 PRINT
110 FOR i=1 TO 1000
120 PRINT words$(i);
130 IF COUNT>65 THEN PRINT
140 NEXT
150 ----
----

=== Syntax

[source,console]
----
<n-var>=COUNT
----

=== Associated Keywords

[source,console]
----
POS
----

[width="100%",cols="50%,>50%",]
|==
a|
== [#data]#DATA#

a|
== D.

|==

A program object which must precede all lists of data for use by the link:bbckey4.html#read[READ] statement.

As for link:bbckey2.html#input[INPUT], string values may be quoted or unquoted. However, quotes need to be used if the string contains commas or leading spaces.

Numeric values may include calculation so long as there are no keywords.

Data items in the list should be separated by a comma.

[source,console]
----
DATA 10.7,2,HELLO," THIS IS A COMMA,",1/3,PRINT
DATA " This is a string with leading spaces."
----

You can use DATA in conjunction with link:bbckey4.html#read[READ] to include data in your program which you may need to change from time to time, but which does not need to be different every time you run the program.

The following example program segment reads through a list of names looking for the name in 'name$'. If the name is found, the name and age are printed. If not, an error message is printed.

[source,console]
----
100 DATA FRED,17,BILL,21,ALLISON,21,NOEL,32
110 DATA JOAN,26,JOHN,19,WENDY,35,ZZZZ,0
120 REPEAT
130 READ list$,age
140 IF list$=name$ THEN PRINT name$,age
150 UNTIL list$=name$ OR list$="ZZZZ"
160 IF list$="ZZZZ" PRINT "Name not in list"
----

=== Syntax

[source,console]
----
DATA <s-const>|<n-const>{,<s-const>|<n-const>}
----

=== Associated Keywords

[source,console]
----
READ, RESTORE
----

== [#def]#DEF#

A program object which must precede declaration of a user defined function (link:bbckey2.html#fn[FN]) or procedure (link:bbckey4.html#proc[PROC]). DEF must be used at the start of a program line.

If DEF is encountered during execution, the rest of the line is ignored. As a consequence, single line definitions can be put anywhere in the program.

Multi-line definitions must not be executed. The safest place to put multi-line definitions is at the end of the main program after the link:#end[END] statement.

There is no speed advantage to be gained by placing function or procedure definitions at the start of the program.

[source,console]
----
DEF FNMEAN ----
DEF PROCJIM ----
----

In order to make the text more readable (always a GOOD THING) the function or procedure name may start with an underline.

[source,console]
----
DEF FN_mean ----
DEF PROC_Jim$ ----
----

Function and procedure names may end with a '$'. This is not compulsory for functions which return strings.

A procedure definition is terminated by the statement link:#endproc[ENDPROC].

A function definition is terminated by a statement which starts with an equals (=) sign. The function returns the value of the expression to the right of the equals sign.

For examples of function and procedure declarations, see link:bbckey2.html#fn[FN] and link:bbckey4.html#proc[PROC]. For a general explanation of functions and procedures, refer to the link:bbc2.html#procedures[Procedures and Functions] sub-section in the link:bbc2.html[General Information] section.

=== Syntax

[source,console]
----
DEF PROC<name>[(<s-var>|<n-var>{,<s-var>|<n-var>})]
DEF FN<name>[(<s-var>|<n-var>{,<s-var>|<n-var>})]
----

=== Associated Keywords

[source,console]
----
ENDPROC, FN, PROC
----

== [#deg]#DEG#

A function which converts radians to degrees.

[source,console]
----
degree_angle=DEG(PI/2)
X=DEG(ATN(1))
----

You can use this function to convert an angle expressed in radians to degrees. One radian is approximately 57 degrees (actually 180/PI). PI/2 radians is 90 degrees and link:bbckey3.html#pi[PI] radians is 180 degrees.

Using DEG is equivalent to multiplying the radian value by 180/PI, but the result is calculated internally to a greater accuracy.

See link:#acs[ACS], link:#asn[ASN] and link:#atn[ATN] for further examples of the use of DEG.

=== Syntax

[source,console]
----
<n-var>=DEG(<numeric>)
----

=== Associated Keywords

[source,console]
----
RAD, SIN, COS, TAN, ACS, ASN, ATN, PI
----

== [#delete]#DELETE#, DEL.

A command which deletes a group of lines from the program. Both start and end lines of the group will be deleted.

You can use DELETE to remove a number of lines from your program. To delete a single line, just type the line number followed by <Enter>.

The example below deletes all the lines between line 10 and 15 (inclusive).

[source,console]
----
DELETE 10,15
----

To delete up to a line from the beginning of the program, use 0 as the first line number. The following example deletes all the lines up to (and including) line 120.

[source,console]
----
DELETE 0,120
----

To delete from a given line to the end of the program, use 65535 as the last line number. To delete from line 2310 to the end of the program, type:

[source,console]
----
DELETE 2310,65535
----

A hyphen is an acceptable alternative to a comma.

=== Syntax

[source,console]
----
DELETE <n-const>,<n-const>
----

=== Associated Keywords

[source,console]
----
EDIT, LIST, OLD, NEW
----

== [#dim]#DIM#

There are two quite different uses for the DIM statement: the first dimensions an array and the second reserves an area of memory for special applications.

=== [#dimarrays]#Dimensioning Arrays#

The DIM statement is used to declare arrays. Arrays must be pre-declared before use and they must not be re-dimensioned. Both numeric and string arrays may be multi dimensional.

[source,console]
----
DIM A(2),Ab(2,3),A$(2,3,4),A%(3,4,5,6)
----

After DIM, all elements in the array are 0/null.

The subscript base is 0, so `DIM X(12)` defines an array of 13 elements.

Arrays are like lists or tables. A list of names is a single dimension array. In other words, there is only one column - the names. Its single dimension in a DIM statement would be the maximum number of names you expected in the table less 1.

If you wanted to describe the position of the pieces on a chess board you could use a two dimensional array. The two dimensions would represent the row (numbered 0 to 7) and the column (also numbered 0 to 7). The contents of each 'cell' of the array would indicate the presence (if any) of a piece and its value.

[source,console]
----
DIM chess_board(7,7)
----

Such an array would only represent the chess board at one moment of play. If you wanted to represent a series of board positions you would need to use a three dimensional array. The third dimension would represent the 'move number'. Each move would use about 320 bytes of memory, so you could record 40 moves in about 12.5k bytes.

[source,console]
----
DIM chess_game(7,7,40)
----

=== [#dimreserving]#Reserving an Area of Memory#

A DIM statement is used to reserve an area of memory which the interpreter will not then use. The variable in the DIM statement is set by BBC BASIC (Z80) to the start address of this memory area. This reserved area can be used by the indirection operators, machine code, etc.

The example below reserves 68 bytes of memory and sets A% equal to the address of the first byte. Thus A%?0 to A%?67 are free for use by the program (68 bytes in all):

[source,console]
----
DIM A% 67
----

A 'link:annexc.html#dimspace[DIM space]' error will occur if a size of less than -1 is used (DIM P% -2). DIM P%-1 is a special case; it reserves zero bytes of memory. This is of more use than you might think, since it tells you the limit of the dynamic variable allocation. Thus,

[source,console]
----
DIM P% -1
PRINT HIMEM-P%
----

is the equivalent of PRINT FREE(0) in some other versions of BASIC. See the link:bbc3.html[Assembler] section for a more detailed description of the use of DIM for reserving memory for machine code programs.

=== Syntax

[source,console]
----
DIM <n-var>|<s-var>(<numeric>{,<numeric>})
DIM <n-var> <numeric>
----

=== Associated Keywords

[source,console]
----
CLEAR
----


== [#div]#DIV#

A binary operation giving the integer quotient of two items. The result is always an integer.

[source,console]
----
X=A DIV B
y=(top+bottom+1) DIV 2
----

You can use this function to give the 'whole number' part of the answer to a division. For example,

[source,console]
----
21 DIV 4
----

would give 5 (with a 'remainder' of 1).

Whilst it is possible to use DIV with real numbers, it is really intended for use with integers. If you do use real numbers, BBC BASIC (Z80) converts them to integers by truncation before DIViding them.

=== Syntax

[source,console]
----
<n-var>=<numeric> DIV <numeric>
----

=== Associated Keywords

[source,console]
----
MOD
----

== [#draw]#DRAW#

A statement which draws a line on the screen in the graphics modes. The statement is followed by the X and Y coordinates of the end of the line.

=== Syntax

[source,console]
----
DRAW <numeric>,<numeric>
----

=== Associated Keywords

[source,console]
----
MODE, PLOT, MOVE, CLG, VDU, GCOL
----

== [#edit]#* EDIT#

A command to edit specified lines. By changing only the line number, you can also use EDIT to duplicate a line.

WARNING: Instead of implementing an EDIT keyword, BBC BASIC (Z80) for the Agon Light delegates this functionality to the MOS command of the same name. Because EDIT is a MOS command, you need to prefix any call to it using `*`. It is very easy to trip up and forget this!

[source,console]
----
*EDIT 230
----

To save your changes and exit the edit mode, press <Enter>.

To abort the single line editor and leave the line unchanged, press <Esc>.

=== Syntax

[source,console]
----
EDIT <l-num>
----

=== Associated Keywords

[source,console]
----
DELETE, LIST, OLD, NEW
----

== [#else]#ELSE#, EL.

A statement delimiter which provides an alternative course of action in link:bbckey2.html#if[IF]...link:bbckey4.html#then[THEN], link:bbckey3.html#on[ON...GOSUB], link:bbckey3.html#on[ON...GOTO] and link:bbckey3.html#on[ON...PROC] statements.

In an link:bbckey2.html#if[IF] statement, if the test is link:bbckey2.html#false[FALSE], the statements after ELSE will be executed. This makes the following work:

[source,console]
----
IF A=B THEN B=C ELSE B=D
IF A=B THEN B=C:PRINT"WWW" ELSE B=D:PRINT"QQQ"
IF A=B THEN B=C ELSE IF A=C THEN------------...
----

In a multi statement line containing more than one link:bbckey2.html#if[IF], the statement(s) after the ELSE delimiter will be actioned if ANY of the tests fail. For instance, the example below would print the error message '`er$`' if 'x' did not equal 3 OR if 'a' did not equal 'b'.

[source,console]
----
IF x=3 THEN IF a=b THEN PRINT a$ ELSE PRINT er$
----

If you want to 'nest' the tests, you should use a procedure call. The following example, would print 'Bad' ONLY if x was equal to 3 AND 'a' was not equal to 'b'.

[source,console]
----
IF x=3 THEN PROC_ab_test
...
DEF PROC_ab_test
IF a=b THEN PRINT a$ ELSE PRINT er$
ENDPROC
----

You can use ELSE with link:bbckey3.html#on[ON...GOSUB], link:bbckey3.html#on[ON...GOTO] and link:bbckey3.html#on[ON...PROC] statements to prevent an out of range control variable causing an 'link:annexc.html#onrange[ON range]' error.

[source,console]
----
ON action GOTO 100, 200, 300 ELSE PRINT "Error"
ON number GOSUB 100,200,300 ELSE PRINT "Error"
ON value PROCa,PROCb,PROCc ELSE PRINT "Error"
----

=== Syntax

[source,console]
----
IF <t-cond> THEN <stmt> ELSE <stmt>
ON <n-var> GOTO <l-num>{,<l-num>} ELSE <stmt>
ON <n-var> GOSUB <l-num>{,<l-num>} ELSE <stmt>
ON <n-var> PROC<name>{,PROC<name>} ELSE <stmt>
----

=== Associated Keywords:

[source,console]
----
IF, THEN, ON
----

== [#end]#END#

A statement causing the interpreter to return to direct mode. There can be any number (>=0) of END statements anywhere in a program. END closes all open data files.

END tells BBC BASIC (Z80) that it has reached the end of the program. You don't have to use END, just 'running out of program' will have the same effect, but it's a bit messy.

You can use END within, for instance, an link:bbckey2.html#if[IF]...link:bbckey4.html#then[THEN]...link:#else[ELSE] statement to stop your program if certain conditions are satisfied. You should also use END to stop BBC BASIC (Z80) 'running into' any procedure or function definitions at the end of your program.

=== Syntax

[source,console]
----
END
----

=== Associated Keywords

[source,console]
----
STOP
----

== [#endproc]#ENDPROC#

A statement denoting the end of a procedure.

All local variables and the dummy arguments are restored at ENDPROC and the program returns to the statement after the calling statement.

=== Syntax

[source,console]
----
ENDPROC
----

=== Associated Keywords

[source,console]
----
DEF, FN, PROC, LOCAL
----

== [#envelope]#ENVELOPE#

A statement which is used, in conjunction with the link:bbckey4.html#sound[SOUND] statement, to control the pitch of a sound whilst it is playing.

*_Not implemented_*

=== Syntax

[source,console]
----
ENVELOPE <numeric>, <numeric>, <numeric>, <numeric>, <numeric>, <numeric>, <numeric>, <numeric>, <numeric>, <numeric>, <numeric>, <numeric>, <numeric>, <numeric>
----

=== Associated Keywords

[source,console]
----
SOUND
----

